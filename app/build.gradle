apply plugin: "com.android.application"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "jacoco-android"

def versionMajor = 4
def versionMinor = 1
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

repositories {
    maven { url 'https://maven.fabric.io/repo' }
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.heinrichreimersoftware.wg_planer"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-retrofit-2.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix ".${versionBuild} DEBUG"
        }
    }
}

dependencies {
    //Support
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:preference-v14:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'

    //Firebase / Play services
    compile 'com.google.firebase:firebase-core:9.0.0'
    compile 'com.google.firebase:firebase-crash:9.0.0'
    compile 'com.google.android.gms:play-services-location:9.0.0'

    //UI / Views
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'com.heinrichreimersoftware:material-drawer:2.2.0'
    compile 'com.jpardogo.materialtabstrip:library:1.1.0'
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.0'
    compile 'jp.wasabeef:recyclerview-animators:1.2.1@aar'

    //Integrations
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'

    //Parsing
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.heinrichreimersoftware:inquiry:3.0.3-beta'

    //Networking
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    compile 'com.squareup.mimecraft:mimecraft:1.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
}

apply plugin: 'com.google.gms.google-services'
